plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'signing'

group 'com.triceracode'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    //RETROFIT2
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    //LOMBOK
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task packageJavadoc(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

artifacts {
    archives sourcesJar, packageJavadoc
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'com.triceracode'
            artifactId = 'poke-api'
            version = '1.0.0'
            artifact sourcesJar
            artifact packageJavadoc
            versionMapping {
                usage('java-api') { fromResolutionOf('runtimeClasspath') }
                usage('java-runtime') { fromResolutionResult() }
            }
            pom {
                name = 'PokeApi OpenSource'
                packaging = 'jar'
                artifactId = 'poke-api'
                description = 'This application is an implementation for PokeApi with a short configuration deployment'
                url = 'https://github.com/AureoGonz/poke-api'

                scm {
                    connection = 'scm:git:git://git@github.com:AureoGonz/poke-api.git'
                    developerConnection = 'scm:git:ssh://git@github.com:AureoGonz/poke-api.git'
                    url = 'https://github.com/AureoGonz/poke-api'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'aureogonz'
                        name = 'Aureo Gonzalez'
                        email = 'triceracode@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

test {
    useJUnitPlatform()
}